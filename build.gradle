buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'


version = "1.12.2-0.2.7.2"
group = "net.dark_roleplay"
archivesBaseName = "drpmedieval"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2756"
    runDir = "run"
    
    mappings = "snapshot_20180709"
    makeObfSourceJar = false
}

repositories {
	//JEI
	maven {
		name = "Progwml6 maven"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "DarkRoleplayMaven"
		url = "https://dark-roleplay.net/files/maven/"
	}
	maven{
		name = "BoP"
		url = "http://files.minecraftforge.net/maven/"
	}
}


configurations {
    // configuration that holds jars to embed inside the jar
    embed
    compile.extendsFrom(embed)
}

dependencies {
// compile against the JEI API but do not include it at runtime
	deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
	// at runtime, use the full JEI jar
	runtime "mezz.jei:jei_${mc_version}:${jei_version}"
  	runtime "com.github.glitchfiend.biomesoplenty:BiomesOPlenty:1.12.2-7.0.1.2399:universal"
	
	deobfProvided "darkroleplay:drpcore:${mc_version}-${drp_core_version}"
	embed "darkroleplay:drplibrary:${mc_version}-${drp_library_version}"
	deobfProvided "darkroleplay.core_modules:drpcmblueprints:${mc_version}-${drp_core_module_blueprints_version}"
	deobfProvided "darkroleplay.core_modules:drpcmdates:${mc_version}-${drp_core_module_dates_version}"
	deobfProvided "darkroleplay.core_modules:drpcmcrops:${mc_version}-${drp_core_module_crops_version}"	
	deobfProvided "darkroleplay.core_modules:drpcmmaarg:${mc_version}-${drp_core_module_maarg_version}"
	deobfProvided "darkroleplay.core_modules:drpcmguis:${mc_version}-${drp_core_module_guis_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//Signing the Jar
task signJar(type: SignJar, dependsOn: reobfJar) {

	if(project.hasProperty('keyStore')){
		keyStore = project.findProperty('keyStore')
		alias = project.findProperty('keyStoreAlias')
		storePass = project.findProperty('keyStorePass')
		keyPass = project.findProperty('keyStoreKeyPass')
		
		inputFile = jar.archivePath
		outputFile = jar.archivePath
	}
}
build.dependsOn signJar

jar{
	into('META-INF/libraries'){
		from configurations.embed
	}

	manifest {
		attributes([
            'ContainedDeps': configurations.embed.collect { it.getName() }.join(' '),
			'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
			'Timestamp': System.currentTimeMillis()
		])
	}
}