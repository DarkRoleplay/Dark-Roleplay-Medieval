buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'


version = "0.2.5"
group = "net.dark_roleplay"
archivesBaseName = "medieval"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.4.2709"
    runDir = "run"
	
    mappings = "snapshot_20171003"
    makeObfSourceJar = false
}

repositories {
	maven {
		name = "DarkRoleplayMaven"
		url = "http://dark-roleplay.net/files/maven/"
	}
	  //JEI
	  maven {
		name = "Progwml6 maven"
		url = "http://dvs1.progwml6.com/files/maven"
	  }
}


configurations {
    // configuration that holds jars to embed inside the jar
    embed
    compile.extendsFrom(embed)
}

dependencies {
	// compile against the JEI API but do not include it at runtime
	deobfProvided "mezz.jei:jei_${mc_version}:${jei_version}:api"
	// at runtime, use the full JEI jar
	runtime "mezz.jei:jei_${mc_version}:${jei_version}"
	
	embed "darkroleplay.library:drplibrary-${mc_version}:${drp_library_version}"
	deobfProvided "darkroleplay.core:drpcore-${mc_version}:${drp_core_version}:SNAPSHOT"
	runtime "darkroleplay.core:drpcore-${mc_version}:${drp_core_version}:SNAPSHOT"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar{
	into('META-INF/libraries'){
		from configurations.embed
	}

	manifest {
		attributes([
            'ContainedDeps': configurations.embed.collect { it.getName() }.join(' '),
			'Maven-Artifact': "${project.group}:${project.archivesBaseName}:${project.version}",
			'Timestamp': System.currentTimeMillis()
		])
	}
}